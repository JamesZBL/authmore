/*
 * Copyright 2019 ZHENG BAO LE
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        maven {
            url = 'https://maven.aliyun.com/repository/public'
        }
    }
}

plugins {
    id "com.gradle.build-scan" version "2.2.1"
    id 'com.bmuschko.docker-spring-boot-application' version '4.8.0'
}

ext {
    registryHost = "registry-vpc.cn-beijing.aliyuncs.com"
    registryRepository = "${registryHost}/letec"
}

allprojects {
    group = 'com.github.jameszbl'
    version = '0.0.1-SNAPSHOT'
}

description = 'Authmore - 轻量级 OAuth2 开放平台开发套件'

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.bmuschko.docker-spring-boot-application'

    repositories {
        mavenLocal()
        maven {
            url = 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url = 'https://repo.spring.io/milestone'
        }
        maven {
            url = 'http://repo.maven.apache.org/maven2'
        }
    }

    docker {
        registryCredentials {
            url = "${registryHost}"
            username = "${System.env.PRIVATE_REG_USERNAME}"
            password = "${System.env.PRIVATE_REG_PASSWORD}"
        }

        springBootApplication {
            baseImage = 'openjdk:8-jdk-alpine'
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    javadoc {
        afterEvaluate {
            configure(options) {
                encoding "UTF-8"
                charSet 'UTF-8'
                author true
                version true
                failOnError false
                links "http://docs.oracle.com/javase/8/docs/api"
            }
        }
    }

    publishing {
        repositories {
            maven {
                def userName = System.getProperty("SONATYPE_USERNAME")
                def passWord = System.getProperty("SONATYPE_PASSWORD")
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username userName
                    password passWord
                }
            }
        }

        publications {

            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'authmore'
                    packaging 'jar'
                    description = 'A lightweight development kit for building OAuth2 open platform.'
                    url = 'https://github.com/jameszbl/authmore'

                    scm {
                        connection = 'scm:git@github.com:Codearte/gradle-nexus-staging-plugin.git'
                        developerConnection = 'scm:git@github.com:Codearte/gradle-nexus-staging-plugin.git'
                        url = 'https://github.com/jameszbl/authmore'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'JamesZBL'
                            name = 'ZHENG BAO LE'
                            email = 'zhengbaole@gmail.com'
                        }
                    }

                    withXml {
                        def root = asNode()
                        root.dependencies.'*'.findAll {
                            def d = it
                            d.scope.text() == 'runtime' && project.configurations.findByName("implementation").allDependencies.find { dep ->
                                dep.name == it.artifactId.text()
                            }.each() {
                                d.scope*.value = 'compile'
                                d.appendNode('optional', true)
                            }
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}